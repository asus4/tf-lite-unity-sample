// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel Init
#pragma kernel LabelToTex

struct Label
{
    float scores[21];
};

uint Width;
uint Height;
StructuredBuffer<Label> LabelBuffer;
StructuredBuffer<float4> ColorTable;
RWTexture2D<float4> Result;

inline int ArgMax(in Label label)
{
    int maxIndex = -1;
    float maxScore = -1.0;

    for (int i = 0; i < 21; ++i)
    {
        if (label.scores[i] > maxScore)
        {
            maxScore = label.scores[i];
            maxIndex = i;
        }
    }
    return maxIndex; 
}

[numthreads(1,1,1)]
void Init (uint2 tid : SV_DispatchThreadID)
{
    Result[tid] = float4(0, 0, 0, 1);
}

[numthreads(8,8,1)]
void LabelToTex (uint2 tid : SV_DispatchThreadID)
{
    uint gid = (Height - 1 - tid.y) * Width + tid.x;
    Label label = LabelBuffer[gid];
    int maxScore = ArgMax(label);
    float4 c = ColorTable[maxScore];
    c.a = 1.0;
    Result[tid] = c;
}
